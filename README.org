#+title: Current Build Process For This Site
#+author: Alex Drysdale
#+date: <2025-04-19 Sat>
#+property: header-args:elisp :tangle "build.el"

This website is built with =org-mode= and hosted through GitHub pages.

GitHub pages expects only a single =index.html= file and builds the site the from the =docs= directory - making the build process extremely simple.

* Building the Home Page

The first thing I want on the home page is a list of articles with the article title as the description.
To do this, I create a cons list of =(title . path)= of each article in the =src/= directory that isn't =index.org=.

Currently, just getting the title via a regular expression seems to work well.
#+begin_src elisp :results none
  (defun get-titles-from-org-files (directory)
    "Extract titles from org files in DIRECTORY."
    (let ((files (directory-files directory t "\\.org$")))
      (mapcar (lambda (file)
                (let ((rel-file (file-relative-name
                                 file (expand-file-name directory))))
                  (with-temp-buffer
                    (insert-file-contents file)
                    (goto-char (point-min))
                    (if (re-search-forward "^#\\+title: \\(.*\\)$" nil t)
                        (cons rel-file (match-string 1))
                      (cons rel-file nil)))))
                files)))
#+end_src

Next the home page (=index.org=) is built. Currently this is really simple and just includes the author's name and a list of the all the articles.

#+begin_src elisp :results none
  (defun build-index (author)
    "Build the index in DIR."
    ;; Copies the README.org to the index.
    (let ((dir "src")
          (index "src/index.org"))
      (with-current-buffer (find-file-noselect index)
        (erase-buffer)
        (insert (format "#+title: %s's Personal Website\n" author))
        (insert "\n* Articles\n")
        (save-buffer))
      (dolist (result (get-titles-from-org-files dir))
        (let ((path (car result))
              (title (cdr result)))
          (with-current-buffer (find-file-noselect index)
            (goto-char (point-max))
            (unless (string= (concat dir "/" path) index)
              (insert (format "- [[file:%s][%s]]\n" path title))
              (save-buffer)))))))
#+end_src

* Publishing the Site

Finally, the site is published using =ox-publish= with this article (the =README.org=) being copied as an article.

#+begin_src elisp
  (require 'ox-publish)
  (let ((org-html-validation-link nil)
        (org-html-head-include-scripts nil)
        (org-html-head-include-default-style nil)
        (org-html-head (concat
                        "<link rel=\"stylesheet\""
                        "href=\"resources/style.css\""
                        "type=\"text/css\" />"
                        "<header><a href=\"index.html\">Home</a></header>\n"
                        ))
        (org-src-fontify-natively t)
        (org-publish-project-alist
         '(("blog"
            :base-directory "src"
            :publishing-directory "docs"
            :auto-sitemap nil
            :recursive t
            :with-author nil
            :with-creator t
            :with-toc t
            :section-numbers nil
            :time-stamp-file nil
            :publishing-function org-html-publish-to-html))))
    (copy-file "README.org" "src/build-process.org" t)
    (build-index "Alex Drysdale")
    (org-publish-all t)
    (message "Site built at %s"
             (format-time-string "%Y-%m-%d %H:%M:%S")))
#+end_src

#+RESULTS:
: Site built at 2025-04-19 16:47:16


* Git Hooks

This script is tangled into =.git/hooks/build.el= which means that we just need to create a =pre-commit= hook that runs the =build.el= file.

#+begin_src bash :results none :tangle ".git/hooks/pre-commit" :eval no
  #!/bin/sh
  emacs --batch -Q --script build.el
#+end_src

and make that file executable:
#+begin_src bash :tangle no :results none
  chmod +x .git/hooks/pre-commit
#+end_src

